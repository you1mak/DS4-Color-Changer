plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
}

val generatedIconResDir = layout.buildDirectory.dir("generated/icon-res")

val copyAppIcon = tasks.register<Copy>("copyAppIcon") {
    from(rootProject.file("assets/icon.png"))
    into(generatedIconResDir.map { it.dir("drawable") })
    rename { "app_icon.png" }
}

android {
    namespace = "com.youmak.ps4led"
    compileSdk = 35

    sourceSets {
        getByName("main") {
            res.srcDir(generatedIconResDir)
        }
    }

    defaultConfig {
        applicationId = "com.youmak.ps4led"
        minSdk = 26
        targetSdk = 35
        versionCode = 1
        versionName = "1.0.0"
    }

    buildTypes {
        release {
            isMinifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
}

tasks.named("preBuild").configure {
    dependsOn(copyAppIcon)
}

dependencies {
    implementation("androidx.core:core-ktx:1.15.0")
    implementation("androidx.appcompat:appcompat:1.7.0")
    implementation("com.google.android.material:material:1.12.0")
}
